# Install lm_sensors package and scan for sensors
class lm_sensors::install (
  Boolean $purge       = false,
  Array   $ignore_purge = [], 
) {
  # Install lm_sensors

  ensure_packages ($::lm_sensors::package, {
    ensure => $::lm_sensors::package_ensure,
    notify => Exec['sensors-detect'],
  })

  # Scan for hardware sensors
  exec { 'sensors-detect':
    command => $lm_sensors::exec_command,
    unless  => "/bin/grep \"Generated by sensors-detect\" ${lm_sensors::config_file}",
    notify  => Service[$lm_sensors::service_name],
    require => Package[$lm_sensors::package],
  }

  # create directory for custom configs
  file { $::lm_sensors::sensorsd_dir:
     ensure  => directory,
     owner   => 'root',
     group   => 'root',
     mode    => '0755',
     purge   => $purge,
     recurse => $purge,
     ignore  => $ignore_purge,
     require => Package[$::lm_sensors::package];
  }
}
